; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "HydraDragonAntivirus"
#define MyAppVersion "0.1"
#define MyAppPublisher "Emirhan Ucan"
#define MyAppURL "https://github.com/HydraDragonAntivirus/HydraDragonAntivirus"
#define MyAppExeName "HydraDragonAntivirusLauncher.exe"
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

; Owlyshield definitions
#define OwlyshieldVersion "1.1.0"
#define AgentName "Owlyshield Service"
#define FsFilter "OwlyshieldRansomFilter"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{9B4A19B2-69BC-4AD2-8781-BDD85DCEF0C9}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf64}\{#MyAppName}
DisableDirPage=yes
ChangesAssociations=yes
DisableProgramGroupPage=yes
DisableReadyPage=yes
DisableReadyMemo=yes
LicenseFile=C:\Users\victim\Documents\HydraDragonAntivirus\LICENSE
InfoBeforeFile=C:\Users\victim\Documents\HydraDragonAntivirus\README.md
OutputDir=C:\Users\victim\Documents\output
OutputBaseFilename=HydraDragonAntivirus
SetupIconFile=C:\Users\victim\Documents\HydraDragonAntivirus\hydradragon\assets\HydraDragonAV.ico
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
DiskSpanning=yes
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
PrivilegesRequired=admin
RestartIfNeededByRun=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkedonce

[Files]
; HydraDragon main files
Source: "C:\Users\victim\Documents\HydraDragonAntivirus\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs;

; External dependencies
Source: "C:\Users\victim\Documents\hydratempinstall\7z2500-x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\VC_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\clamav-1.4.3.win.x64.msi"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\npcap-1.83.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\Suricata-8.0.0-windivert-1-64bit.msi"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\jre-8u461-windows-x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\jdk-21_windows-x64_bin.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\windowsdesktop-runtime-8.0.18-win-x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
; Node.js 22.18.0 (x64) MSI
Source: "C:\Users\victim\Documents\hydratempinstall\node-v22.18.0-x64.msi"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\python-3.12.10-amd64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\Sandboxie-Classic-x64-v5.71.2.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "C:\Users\victim\Documents\hydratempinstall\setup.bat"; DestDir: "{tmp}"; Flags: deleteafterinstall

[Dirs]
Name: "{app}\hydradragon\Owlyshield\debug"
Name: "{app}\hydradragon\Owlyshield\utils"
Name: "{app}\hydradragon\Owlyshield\log"; Flags: uninsneveruninstall
Name: "{app}\hydradragon\Owlyshield\config"; Flags: uninsneveruninstall
Name: "{app}\hydradragon\Owlyshield\config\threats"; Flags: uninsneveruninstall

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

; Owlyshield Registry Settings
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "NUM_VERSION"; ValueData: {#OwlyshieldVersion}; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "PROCESS_ACTIVITY_PATH"; ValueData: "{app}\hydraddragon\Owlyshield\debug"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "UTILS_PATH"; ValueData: "{app}\hydradragon\Owlyshield\utils"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "CONFIG_PATH"; ValueData: "{app}\hydradragon\Owlyshield\config"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "LOG_PATH"; ValueData: "{app}\hydradragon\Owlyshield\log"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "APP_ID"; ValueData: "8C19967B-1D27-4E6A-85CD-5059912C2788"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "LANGUAGE"; ValueData: "en-US"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "KILL_POLICY"; ValueData: "DO_NOTHING"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "TELEMETRY"; ValueData: 0; Flags: uninsdeletekey
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\RunOnce"; ValueType: string; ValueName: "OwlyshieldPostInstall"; ValueData: """{app}\post_install.bat"""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
; Enable test signing mode for unsigned drivers
Filename: "bcdedit.exe"; Parameters: "/set testsigning on"; Flags: runhidden

; 7-Zip (silent)
Filename: "{tmp}\7z2500-x64.exe"; Parameters: "/S"; Flags: shellexec waituntilterminated

; .NET Desktop Runtime 8.0.18 (silent, no restart)
Filename: "{tmp}\windowsdesktop-runtime-8.0.18-win-x64.exe"; Parameters: "/install /quiet /norestart"; Flags: shellexec waituntilterminated

; Visual C++ Redistributable 2022 (silent, auto-install)
Filename: "{tmp}\VC_redist.x64.exe"; Parameters: "/install /quiet /norestart"; Flags: shellexec waituntilterminated

; JRE 8u461 (silent, updates enabled)
Filename: "{tmp}\jre-8u461-windows-x64.exe"; Parameters: "/s INSTALL_SILENT=Enable AUTO_UPDATE=Enable WEB_JAVA=Disable SPONSORS=Disable"; Flags: shellexec waituntilterminated

; JDK 21 (silent, environment variables)
Filename: "{tmp}\jdk-21_windows-x64_bin.exe"; Parameters: "/s"; Flags: shellexec waituntilterminated

; Python 3.12.10 amd64 (silent, all users, add to PATH, include pip)
Filename: "{tmp}\python-3.12.10-amd64.exe"; Parameters: "/quiet InstallAllUsers=1 PrependPath=1 Include_pip=1"; Flags: shellexec waituntilterminated

; ClamAV Installer (MSI - Silent Install)
Filename: "{tmp}\clamav-1.4.3.win.x64.msi"; Parameters: "/quiet /norestart"; Flags: shellexec waituntilterminated

; Suricata Installer (MSI - Silent Install)
Filename: "{tmp}\Suricata-8.0.0-windivert-1-64bit.msi"; Parameters: "/quiet /norestart"; Flags: shellexec waituntilterminated

; Node.js 22.18.0 (silent)
Filename: "{tmp}\node-v22.18.0-x64.msi"; Parameters: "/quiet /norestart"; Flags: shellexec waituntilterminated

; Npcap
Filename: "{tmp}\npcap-1.83.exe"; Flags: shellexec waituntilterminated

; Sandboxie Classic
Filename: "{tmp}\Sandboxie-Classic-x64-v5.71.2.exe"; Flags: shellexec waituntilterminated

; Custom setup script
Filename: "{tmp}\setup.bat"; Flags: shellexec waituntilterminated

[UninstallRun]
; Disable test signing mode on uninstall
Filename: "bcdedit.exe"; Parameters: "/set testsigning off"

; Owlyshield cleanup
Filename: "sc.exe"; Parameters: "stop ""{#AgentName}"""
Filename: "sc.exe"; Parameters: "stop ""{#FsFilter}"""
Filename: "sc.exe"; Parameters: "delete ""{#AgentName}"""
Filename: "sc.exe"; Parameters: "delete ""{#FsFilter}"""
Filename: "del.exe"; Parameters: """C:\Windows\System32\drivers\{#FsFilter}.sys"""